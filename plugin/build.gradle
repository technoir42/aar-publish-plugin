plugins {
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.10.1"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "maven-publish"
}

group = "com.github.technoir42"
version = "1.0.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId = "aar-publish-plugin"

            pom.withXml {
                asNode().dependencies.dependency.each {
                    if (it.groupId.text() == "com.android.tools.build" && it.artifactId.text() == "gradle") {
                        it.scope*.value = "provided"
                    }
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        aarPublishPlugin {
            id = "com.github.technoir42.aar-publish"
            displayName = "AAR Publish Plugin"
            description = "Plugin for publishing Android (AAR) libraries using Maven Publish plugin"
            implementationClass = "com.github.technoir42.plugin.aarpublish.AarPublishPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/technoir42/aar-publish-plugin"
    vcsUrl = "https://github.com/technoir42/aar-publish-plugin.git"
    tags = ["android", "maven", "aar"]
}

dependencies {
    implementation "com.android.tools.build:gradle:3.3.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    compileOnly "javax.inject:javax.inject:1"

    testCompileOnly "junit:junit:4.12"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.2"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.4.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.4.2"
    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.8"
}
